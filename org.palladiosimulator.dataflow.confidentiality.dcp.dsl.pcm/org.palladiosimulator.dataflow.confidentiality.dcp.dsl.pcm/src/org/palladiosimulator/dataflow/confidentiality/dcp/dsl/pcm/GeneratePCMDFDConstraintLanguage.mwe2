module org.palladiosimulator.dataflow.confidentiality.dcp.dsl.pcm.GeneratePCMDFDConstraintLanguage

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
		platformUri = rootPath
		
		uriMap = {
			from = "platform:/plugin/de.uka.ipd.sdq.identifier/model/identifier.genmodel"
			to = "platform:/resource/de.uka.ipd.sdq.identifier/model/identifier.genmodel"
		}
		uriMap = {
			from = "platform:/plugin/de.uka.ipd.sdq.identifier/model/identifier.ecore"
			to = "platform:/resource/de.uka.ipd.sdq.identifier/model/identifier.ecore"
		}
		uriMap = {
			from = "platform:/plugin/org.palladiosimulator.pcm/model/pcm.genmodel"
			to = "platform:/resource/org.palladiosimulator.pcm/model/pcm.genmodel"
		}
		uriMap = {
			from = "platform:/plugin/org.palladiosimulator.pcm/model/pcm.ecore"
			to = "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore"
		}
		
		uriMap = {
			from = "platform:/plugin/org.eclipse.emf.codegen.ecore/model/GenModel.genmodel"
			to = "platform:/resource/org.eclipse.emf.codegen.ecore/model/GenModel.genmodel"
		}
		uriMap = {
			from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.genmodel"
			to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
		}
		uriMap = {
			from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.ecore"
			to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"
		}
		uriMap = {
			from = "platform:/plugin/org.eclipse.uml2.codegen.ecore/model/GenModel.genmodel"
			to = "platform:/resource/org.eclipse.uml2.codegen.ecore/model/GenModel.genmodel"
		}
		
		uriMap = {
			from = "platform:/plugin/de.sebinside.dcp.dsl/model/generated/DSL.genmodel"
			to = "platform:/resource/de.sebinside.dcp.dsl/model/generated/DSL.genmodel"
		}
		uriMap = {
				from = "platform:/plugin/de.sebinside.dcp.dsl/model/generated/DSL.ecore"
				to = "platform:/resource/de.sebinside.dcp.dsl/model/generated/DSL.ecore"
		}
		uriMap = {
			from = "platform:/plugin/org.palladiosimulator.dataflow.confidentiality.pcm.model/model/dataFlowConfidentiality.genmodel"
			to = "platform:/resource/org.palladiosimulator.dataflow.confidentiality.pcm.model/model/dataFlowConfidentiality.genmodel"
		}
		uriMap = {
			from = "platform:/plugin/org.palladiosimulator.dataflow.confidentiality.pcm.model/model/dataFlowConfidentiality.ecore"
			to = "platform:/resource/org.palladiosimulator.dataflow.confidentiality.pcm.model/model/dataFlowConfidentiality.ecore"
		}
		
		registerGenModelFile = "platform:/resource/org.eclipse.emf.codegen.ecore/model/GenModel.genmodel"
		registerGenModelFile = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
		registerGenModelFile = "platform:/resource/org.eclipse.uml2.codegen.ecore/model/GenModel.genmodel"
		registerGeneratedEPackage = "org.eclipse.uml2.codegen.ecore.genmodel.GenModelPackage"
		
		registerGenModelFile = "platform:/resource/de.uka.ipd.sdq.identifier/model/identifier.genmodel"
		registerGenModelFile = "platform:/resource/org.palladiosimulator.pcm/model/pcm.genmodel"
		registerGenModelFile = "platform:/resource/de.sebinside.dcp.dsl/model/generated/DSL.genmodel"
		registerGenModelFile = "platform:/resource/org.palladiosimulator.dataflow.confidentiality.pcm.model/model/dataFlowConfidentiality.genmodel"
	}
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.palladiosimulator.dataflow.confidentiality.dcp.dsl.pcm"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "org.palladiosimulator.dataflow.confidentiality.dcp.dsl.pcm.PCMDFDConstraintLanguage"
			fileExtensions = "DCPDSL"

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = true
			}
			junitSupport = {
				junitVersion = "5"
			}
			fragment =  scoping.ImportNamespacesScopingFragment2 {}
		}
	}
}
