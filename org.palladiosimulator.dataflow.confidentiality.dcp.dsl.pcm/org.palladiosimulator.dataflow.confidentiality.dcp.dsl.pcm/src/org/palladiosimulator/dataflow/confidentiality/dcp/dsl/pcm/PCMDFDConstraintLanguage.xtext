grammar org.palladiosimulator.dataflow.confidentiality.dcp.dsl.pcm.PCMDFDConstraintLanguage 
with de.sebinside.dcp.dsl.DSL 

generate pCMDFDConstraintLanguage "http://www.palladiosimulator.org/dataflow/confidentiality/dcp/dsl/pcm/PCMDFDConstraintLanguage"

import "http://www.sebinside.de/dcp/dsl/DSL" as base 

import "platform:/resource/org.palladiosimulator.dataflow.confidentiality.pcm.model/model/dataFlowConfidentiality.ecore#//characteristics" as pcmDictionary
import "platform:/resource/org.palladiosimulator.dataflow.confidentiality.pcm.model/model/dataFlowConfidentiality.ecore#//repository" as pcmConfRepository

import "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore#//allocation" as allocation
import "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore#//usagemodel" as usagemodel
import "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore#//core/composition" as composition
import "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore#//core/entity" as entity
import "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore#//repository" as repository
import "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore#//seff" as seff


@Override
Model: //necessary
	(targetModelType=TargetModelTypeDef)?
	(elements+=AbstractElement)*;
	
@Override
TargetModelTypeDef:
	'target' type="PCMDFD" 'using' pcmTypeContainer=[pcmDictionary::CharacteristicTypeDictionary] 
		(',' usageModel=[usagemodel::UsageModel] ',' repositoryModel=[repository::Repository])?
;
//	
//PCMTargetModelTypeDef:
//	'target' type="PCMDFD" 'using' pcmTypeContainer=[pcmDictionary::CharacteristicTypeDictionary] 
//		',' usageModel=[usagemodel::UsageModel] ',' repositoryModel=[repository::Repository]
//;

//@Override	
//DestinationSelector returns base::DestinationSelector:
//	PropertySelector | PropertyClassSelector | ComponentIdentitySelector;

@Override
PropertySelector:
	'component.property.' ref=CharacteristicTypeSelector;

@Override
PropertyClassSelector:
	'component.class.' ref=[base::CharacteristicClass];

@Override
NodeIdentitiySelector:
	'component.name.' name=STRING 
	| 
	'component.identity.' (
		'Component.' assembly=[composition::AssemblyContext] '.' component=[repository::BasicComponent] |
		'Action.' action=[seff::AbstractAction] |
		'SEFF.' assembly=[composition::AssemblyContext] '.' component=[repository::BasicComponent] '.' signature=[repository::OperationSignature] |
		'UserAction.' userAction=[usagemodel::EntryLevelSystemCall] |
		'Store.' assembly=[composition::AssemblyContext] '.' store=[pcmConfRepository::OperationalDataStoreComponent])
	;
